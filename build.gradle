buildscript {
    repositories {
        mavenLocal()
        gradlePluginPortal()
        mavenCentral()
        jcenter()
        maven { 
            url "https://repo.spring.io/libs-milestone" 
            mavenContent { releasesOnly() }
        }
        maven { 
            url "https://repo.spring.io/libs-snapshot" 
            mavenContent { snapshotsOnly() }
        }
        maven { 
            url "https://plugins.gradle.org/m2/" 
            mavenContent { releasesOnly() }
        }
    }
    dependencies {
        classpath "de.undercouch:gradle-download-task:${project.gradleDownloadTaskVersion}"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${project.springBootVersion}"
        classpath "gradle.plugin.com.google.cloud.tools:jib-gradle-plugin:${project.jibVersion}"
        classpath "io.freefair.gradle:maven-plugin:${project.gradleMavenPluginVersion}"
        classpath "io.freefair.gradle:lombok-plugin:${project.gradleLombokPluginVersion}"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.1.1"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { 
        url "https://oss.sonatype.org/content/repositories/snapshots" 
        mavenContent { snapshotsOnly() }
    }
    maven { 
        mavenContent { releasesOnly() }
        url "https://build.shibboleth.net/nexus/content/repositories/releases/" 
    }
    maven { 
        mavenContent { releasesOnly() }
        url "https://repo.spring.io/milestone/" 
    }
    maven { 
        url "https://repo.spring.io/snapshot/" 
        mavenContent { snapshotsOnly() }
    }
    maven { 
        mavenContent { snapshotsOnly() }
        url "https://oss.jfrog.org/artifactory/oss-snapshot-local" 
    }
}

def casServerVersion = project.'cas.version'
def casWebApplicationBinaryName = "cas.war"

project.ext."casServerVersion" = casServerVersion
project.ext."casWebApplicationBinaryName" = casWebApplicationBinaryName

apply plugin: "war"
apply plugin: "io.freefair.war-overlay"

apply from: rootProject.file("gradle/tasks.gradle")
apply from: rootProject.file("gradle/springboot.gradle")
apply from: rootProject.file("gradle/dockerjib.gradle")

apply plugin: "io.freefair.lombok"
lombok {
    config["config.stopBubbling"] = "true"
    config["lombok.addLombokGeneratedAnnotation"] = "true"
    config["lombok.equalsAndHashCode.doNotUseGetters"] = "true"
    config["lombok.log.fieldName"] = "LOGGER"
    config["lombok.log.fieldIsStatic"] = "true"
    config["lombok.toString.doNotUseGetters"] = "true"
}

apply plugin: "eclipse"
eclipse {
    classpath {
        downloadSources = true
        downloadJavadoc = true
    }
}

apply plugin: "idea"
idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

apply plugin: "org.sonarqube"

dependencies {
    // Other CAS dependencies/modules may be listed here...

    // Authentication
    implementation "org.apereo.cas:cas-server-core-authentication:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-core-authentication-api:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-core-authentication-attributes:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-core-authentication-mfa-api:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-core-authentication-throttle:${project.'cas.version'}"
    // Cookie
    implementation "org.apereo.cas:cas-server-core-cookie-api:${project.'cas.version'}"
    // Services
    implementation "org.apereo.cas:cas-server-core-services:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-core-services-api:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-core-services-registry:${project.'cas.version'}"
    // Ticket
    implementation "org.apereo.cas:cas-server-core-tickets-api:${project.'cas.version'}"
    // Core Utilities
    implementation "org.apereo.cas:cas-server-core-util-api:${project.'cas.version'}"
    // Validation
    implementation "org.apereo.cas:cas-server-core-validation-api:${project.'cas.version'}"
    // Web
    implementation "org.apereo.cas:cas-server-core-web-api:${project.'cas.version'}"
    // Web Flow
    implementation "org.apereo.cas:cas-server-core-webflow:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-core-webflow-api:${project.'cas.version'}"
    // Actions
    implementation "org.apereo.cas:cas-server-support-actions:${project.'cas.version'}"
    // JDBC Drivers
    implementation "org.apereo.cas:cas-server-support-jdbc-drivers:${project.'cas.version'}"
    // JPA Ticket Registry
    implementation "org.apereo.cas:cas-server-support-jpa-ticket-registry:${project.'cas.version'}"
    // JPA Utilities
    implementation "org.apereo.cas:cas-server-support-jpa-util:${project.'cas.version'}"
    // JSON Service Registry
    implementation "org.apereo.cas:cas-server-support-json-service-registry:${project.'cas.version'}"
    // OAuth 2.0 Server
    implementation "org.apereo.cas:cas-server-support-oauth-api:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-oauth-core-api:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-oauth-services:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-oauth-webflow:${project.'cas.version'}"
    // Pac4j
    implementation "org.apereo.cas:cas-server-support-pac4j-api:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-pac4j-core:${project.'cas.version'}"
    implementation "org.apereo.cas:cas-server-support-pac4j-webflow:${project.'cas.version'}"
    // Token
    implementation "org.apereo.cas:cas-server-support-token-core-api:${project.'cas.version'}"

    // Tomcat Catalina
    implementation "org.apache.tomcat:tomcat-catalina:${springBootTomcatVersion}"
    // Hibernate
    implementation "org.hibernate:hibernate-core:${hibernateCoreVersion}"
    // Google GSON
    implementation "com.google.code.gson:gson:${gsonVersion}"
    // Javascript Object Signing and Encryption (JOSE) and JSON Web Tokens (JWT)
    implementation "com.nimbusds:nimbus-jose-jwt:${nimbusJoseVersion}"
    // Apache HttpComponents Client Fluent API
    implementation "org.apache.httpcomponents:fluent-hc:${fluentHcVersion}"
    // Jasig CAS Client
    implementation "org.jasig.cas.client:cas-client-core:${casClientVersion}"
}

tasks.findByName("jibDockerBuild")
    .dependsOn(copyWebAppIntoJib, copyConfigIntoJib)
    .finalizedBy(deleteWebAppFromJib)

tasks.findByName("jib")
    .dependsOn(copyWebAppIntoJib, copyConfigIntoJib)
    .finalizedBy(deleteWebAppFromJib)

configurations.all {
    resolutionStrategy {
        cacheChangingModulesFor 0, "seconds"
        cacheDynamicVersionsFor 0, "seconds"

        preferProjectModules()

        def failIfConflict = project.hasProperty("failOnVersionConflict") && Boolean.valueOf(project.getProperty("failOnVersionConflict"))
        if (failIfConflict) {
            failOnVersionConflict()
        }
    }
}
